buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply from: "$rootDir/gradle/versioning.gradle"

ext {
    bintrayBaseUrl = 'https://api.bintray.com/maven'
    bintrayRepository = 'maven'
    bintrayPackage = 'thucydides-jbehave'
}
sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'net.thucydides'

repositories {
    mavenLocal()
    jcenter()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

test {
    include '**/When*'
    exclude '**/*$*'
    exclude '**/integration/**'
    exclude '**/samples/**'
    exclude '**/*Sample*'
    maxParallelForks = 4
}

jar {
    manifest {
        attributes("Implementation-Title": "Thucydides JBehave Plugin",
                   "Implementation-Version": project.version.toString())
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

dependencies {
    compile 'net.thucydides:thucydides-core:0.9.275'
    compile 'org.jbehave:jbehave-core:3.9.3'
    compile 'de.codecentric:jbehave-junit-runner:1.1.0'
    compile 'org.reflections:reflections:0.9.8'
    compile 'com.googlecode.lambdaj:lambdaj:2.3.3'
    compile("org.codehaus.groovy:groovy-all:2.3.3")

    testCompile "ch.qos.logback:logback-classic:1.0.13"
    testCompile 'org.easytesting:fest-assert:1.4'
    testCompile 'net.thucydides:thucydides-test-utils:0.9.275'
    testCompile 'net.thucydides:thucydides-sample-alternative-resources:0.9.275'
    testCompile 'org.springframework:spring-test:3.1.2.RELEASE'
    testCompile 'org.springframework:spring-beans:3.1.2.RELEASE'
    testCompile 'org.springframework:spring-context:3.1.2.RELEASE'
    testCompile 'org.springframework:spring-context-support:3.1.2.RELEASE'
    testCompile "junit:junit:4.11"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'thucydides-jbehave'
                    description 'Thucydides JBehave integration'
                    url 'https://github.com/thucydides-webtests/thucydides-jbehave'
                    scm {
                        url 'scm:git:git@github.com:thucydides-webtests/thucydides-jbehave.git'
                        connection 'scm:git:git@github.com:thucydides-webtests/thucydides-jbehave.git'
                        developerConnection 'scm:git:git@github.com:thucydides-webtests/thucydides-jbehave.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'johnsmart'
                            name 'John Ferguson Smart'
                            email 'john.smart@wakaleo.com'
                        }
                    }
                }
            }
        }
    }
}

task createPom << {
    pom {
        project {
            groupId 'org.example'
            artifactId 'test'
            version '1.0.0'

            inceptionYear '2008'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("pom.xml")
}

bintray {
    user = bintrayUsername //this usually comes form gradle.properties file in ~/.gradle
    key = bintrayApiKey //this usually comes form gradle.properties file in ~/.gradle
    publications = ['mavenJava'] // see publications closure
    pkg {
        repo = 'maven'
        userOrg = 'thucydides'
        name = 'thucydides-jbehave'
        desc = 'Thucydides JBehave plugin'
        licenses = ['Apache-2.0']
        labels = ['thucydides','jbehave','bdd']
    }
}
